---
title: 王爽汇编语言第三版实验6
date: 2024-10-04 23:56:12
tags: 汇编语言
---

## 前言

本系列的文章是对王爽老师的汇编语言中的实验的解答记录，原书一共有17个实验，由于学校的教学流程只做到了第12个实验，因此本文章只会有前十二个实验的解答记录,还有个比较重要的是，文章中会有原书实验中没有的题目，这些是我的老师自己加的，你不感兴趣就无视。btw，这也算记录我汇编语言的学习历程了。

### 目录

* {% post_link '汇编语言/王爽汇编语言第三版实验1' '王爽汇编语言第三版实验1' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验2' '王爽汇编语言第三版实验2' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验3' '王爽汇编语言第三版实验3' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验4' '王爽汇编语言第三版实验4' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验5' '王爽汇编语言第三版实验5' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验6' '王爽汇编语言第三版实验6' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验7' '王爽汇编语言第三版实验7' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验8' '王爽汇编语言第三版实验8' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验9' '王爽汇编语言第三版实验9' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验10' '王爽汇编语言第三版实验10' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验11' '王爽汇编语言第三版实验11' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验12' '王爽汇编语言第三版实验12' %}


## 实验任务

1.   编程完成问题7.9中的程序，要求使用至少两种不同的寻址方式实现。

编程，将 datasg 段中每个单词的前 4 个字母改为大写字母。

```assembly
assume cs:codesg,ds:datasg,ss:stacksg
stacksg segment
            dw 0,0,0,0,0,0,0,0
stacksg ends
datasg segment
           db '1. display      '
           db '2. brows        '
           db '3. replace      '
           db '4. modify       '
datasg ends
codesg segment
    start: 
codesg ends
end start

```



第一种方法采用基址变址寻址法进行解决

```assembly
assume cs:codesg,ss:stacksg,ds:datasg
stacksg segment
            dw 0,0,0,0,0,0,0,0
stacksg ends
datasg segment
           db '1. display      '
           db '2. brows        '
           db '3. replace      '
           db '4. modify       '
datasg ends
codesg segment
    start: 
    ;初始化数据段与栈段
           mov  ax,datasg
           mov  ds,ax
           mov  ax,stacksg
           mov  ss,ax
           mov  sp,10H
    ;将数据段作为一个4*16的二维数组
           mov  bx,0            ;将bx初始化为0，用于定位二维数组的第一维
           mov  cx,4            ;循环4次，修改四行数据
    s:     push cx              ;将cx入栈，保存第一层循环的状态
           mov  cx,4            ;将cx赋值为4，改写每行数据的四个数据
 ;si赋值为3，用于定位数组的第二维，3指向第一个小写字母
           mov  si,
    ;嵌套第二层循环
    s1:    mov  al,[bx+si]      ;取出小写字母，使用 [bx+si] 的寻址方式
           and  al,11011111b    ;与运算，运算结果为对应的大写字母
           mov  [bx+si],al      ;将运算结果送回数据段
           inc  si              ;si自增，指向下一个字母
           loop s1
    ;嵌套的第二次循环结束
           pop  cx              ;还原cx的状态到第一层循环
           add  bx,16           ;bx加16，指向下一行
           loop s
           mov  ax,4c00h
           int  21H
codesg ends
end start

```



运行程序前：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/3ee1fea74568f09fba854b31e81bda24.png)

运行程序后： 
![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/001833584411b33d804ef8039343fdb4.png)

第二种方法采用相对基址变址寻址法进行解决

```assembly
assume cs:codesg,ss:stacksg,ds:datasg
stacksg segment
            dw 0,0,0,0,0,0,0,0
stacksg ends
datasg segment
           db '1. display      '
           db '2. brows        '
           db '3. replace      '
           db '4. modify       '
datasg ends
codesg segment
    start: 
           mov  ax,datasg
           mov  ds,ax
           mov  ax,stacksg
           mov  ss,ax
           mov  sp,10H
           mov  bx,0
           mov  cx,4
    s:     push cx
           mov  cx,4
           mov  si,0            ;无需初始化si指向第一个字母
    s1:    mov  al,[bx+si+3]    ;使用 [bx+si+idata] 寻址方式
           and  al,11011111b
           mov  [bx+si+3],al    ;使用 [bx+si+idata] 寻址方式
           inc  si
           loop s1
           pop  cx
           add  bx,16
           loop s
           mov  ax,4c00h
           int  21H
codesg ends
end start

```



运行程序前：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/ace084094c647d04edd3d57f67a181b5.png)

运行程序后： 
							![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/3783c0c04f2b775d6f2936007833314f.png)

2.   有如下定义

DATA SEGMENT

DW 1,1,18 DUP (0)

DATA SEGMENT

请编写程序将裴波那契数列的前20项补充完整。

```assembly
assume cs:codesg,ss:stacksg,ds:datasg
stacksg segment
            dw 0,0,0,0,0,0,0,0
stacksg ends
datasg segment
           dw 20 dup(0)
datasg ends
codesg segment
    start: 
    ;初始化数据段和栈段
           mov  ax,datasg
           mov  ds,ax
           mov  ax,stacksg
           mov  ss,ax
           mov  sp,10H
    ;输入斐波那契函数的前两项
           mov  ax,1
           mov  ds:[0],ax
           mov  ds:[2],ax
    ;将斐波那契函数的前两项入栈
           push ax
           push ax
           mov  cx,18         ;计算前二十位
           mov  bx,4          ;从第三个字开始存储计算结果
    ;设当前计算第i项
    s:     mov  dx,0          ;初始化dx用于计算
           pop  ax            ;将i-1项出栈
           pop  dx            ;将i-2项出栈
           add  dx,ax         ;i-1项与i-2项相加
           push ax            ;将i-1项入栈
           push dx            ;将i项入栈
           mov  ds:[bx],dx    ;将计算结果存入数据段
           add  bx,2          ;bx自增,i++,偏移量指向下一个字
           loop s
           mov  ax,4c00h
           int  21H
codesg ends
end start

```




程序运行前数据段：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/aaf1d9792f2f0ae2cbb8cf9c5682e3c4.png)

程序运行后数据段：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/c1eba182512325b62c03a981fe82794a.png)

数列1到20项的十进制分别为：1 1 2 3 5 8 13 21 34 55 89 144 233 377 610 987 1597 2584 4181 6765 ，

3.   猴子第一天摘下若干个桃子，当即吃了一半，不过瘾，又多吃了一个；第二天，吃剩下的桃子的一半，还不过瘾，又多吃了一个；以后每天都吃前一天剩下的一半多一个，到第十天，只剩下一个桃子。求这十天猴子每天拥有的桃子个数。

```assembly
assume cs:codesg,ss:stacksg,ds:datasg
stacksg segment
               dw 0,0,0,0,0,0,0,0
stacksg ends
datasg segment
              dw 16 dup(0)
datasg ends
codesg segment
       start: 
       ;初始化数据段和栈段
              mov  ax,datasg
              mov  ds,ax
              mov  ax,stacksg
              mov  ss,ax
              mov  sp,10H
              mov  ax,1
              mov  si,18            ;偏移量指向第10个字
              mov  ds:[si],ax       ;存下第十天的数量
              push ax               ;将第十天的数量入栈
              mov  cx,9             ;循环9次计算前九天的数量
       s:     sub  si,2             ;si减2，指向上一个字
              pop  ax               ;将前一天的数量出栈
              inc  ax
              mov  dx,ax
              add  dx,ax            ;前一天的数量加一后乘以2得到结果
              push dx               ;将计算结果入栈用于下一次计算
              mov  ds:[si],dx       ;存下结果
              loop s
              mov  ax,4c00h
              int  21H
codesg ends
end start

```



程序运行前数据段：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/51b30b1e73d6218a574ae932c3d7a250.png)

程序运行后数据段：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/aa7c8164a7853bf789e52364652c5fdb.png)

将上图数据段的内容翻译成十进制可得桃子数量分别是

1534 766 382 190 94 46 22 10 4 1

