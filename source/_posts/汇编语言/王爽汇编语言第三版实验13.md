---
title: 王爽汇编语言第三版实验13
date: 2024-10-05 00:45:30
tags: 汇编语言
---

### 目录

* {% post_link '汇编语言/王爽汇编语言第三版实验1' '王爽汇编语言第三版实验1' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验2' '王爽汇编语言第三版实验2' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验3' '王爽汇编语言第三版实验3' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验4' '王爽汇编语言第三版实验4' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验5' '王爽汇编语言第三版实验5' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验6' '王爽汇编语言第三版实验6' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验7' '王爽汇编语言第三版实验7' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验8和9' '王爽汇编语言第三版实验8和9' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验10和11' '王爽汇编语言第三版实验10和11' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验12' '王爽汇编语言第三版实验12' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验13' '王爽汇编语言第三版实验13' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验14' '王爽汇编语言第三版实验14' %}



## 实验任务

编写、应用中断例程（课本实验13）

1.   编写并安装int 7ch中断例程，功能为显示一个用0结束的字符串，中断例程安装在0:200处。

参数：(dh)=行号，(dl)=列号，(cl)=颜色，ds:si指向字符串首地址。

```assembly
assume cs:code
data segment
         db 'welcome to masm!',0
data ends
code segment
    start:
          mov  ax,cs
          mov  ds,ax
          mov  si,offset dp
          mov  ax,0
          mov  es,ax
          mov  di,200h
          mov  cx,offset dpend-offset dp    ;安装中断例程
          cld
          rep  movsb
          mov  word ptr es:[7ch*4],200h
          mov  word ptr es:[7ch*4+2],0      ;设置中断向量表

          mov  dh,10
          mov  dl,10
          mov  cl,2
          mov  ax,data
          mov  ds,ax                        ;测试int 7ch
          mov  si,0
          int  7ch
          mov  ax,4c00h
          int  21h
    dp:   
          mov  al,160
          mul  dh
          add  dl,dl
          mov  dh,0
          add  ax,dx
          mov  di,ax
          mov  ax,0b800h
          mov  es,ax
    ;中断例程
    s:    
          mov  al,ds:[si]
          mov  ah,0
          cmp  ax,0
          je   f
          mov  ah,cl
          mov  es:[di],ax
          inc  si
          inc  di
          inc  di
          jmp  s
    f:    
          iret
    dpend:nop
code ends
end start


```

结果：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/85c8948aed7ec9ebebd14f6dcaf9dbd9.png)

2.   编写并安装int 7ch中断例程，功能为完成loop指令的功能。

参数：(cx)=循环次数，(bx)=位移

```assembly
assume cs:code
code segment
      start:
            mov  ax,cs
            mov  ds,ax
            mov  si,offset lp
            mov  ax,0
            mov  es,ax
            mov  di,200h
            mov  cx,offset lpend-offset lp      ;安装中断例程
            cld
            rep  movsb
            mov  word ptr es:[7ch*4],200h
            mov  word ptr es:[7ch*4+2],0        ;设置中断向量表

            mov  ax,0b800h
            mov  es,ax
            mov  di,160*12
            mov  bx,offset s-offset se
            mov  cx,80
      s:    
            mov  byte ptr es:[di],'!'           ;测试int 7ch
            add  di,2
            int  7ch
      se:   
            nop
            mov  ax,4c00h
            int  21h
      lp:   
            push bp

            dec  cx
            jcxz f
            mov  bp,sp
            add  [bp+2],bx                      ;中断例程
      f:    
            pop  bp
            iret
      lpend:nop
code ends
end start
```

结果：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/8187d1147530d827096bdfdbae71b1d5.png)

3.   下面的程序，分别在屏幕的第2、4、6、8行显示四句英文诗，补全程序。

```assembly
assume cs:code
code segment
    s1:   db   'Good,better,best,','$'
    s2:   db   'Never let it rest,','$'
    s3:   db   'Till good is better,','$'
    s4:   db   'And better,best.','$'
    s:    dw   offset s1,offset s2,offset s3,offset s4
    row:  db   2,4,6,8
    start:
          mov  ax,cs
          mov  ds,ax
          mov  bx,offset s
          mov  si,offset row
          mov  cx,4
    ok:   mov  bh,0
          mov  dh,[si]
          mov  dl,0
          mov  ah,2
          int  10h
          mov  dx,[bx]
          mov  ah,9
          int  21h
          inc  si
          add  bx,2
          loop ok
          mov  ax,4c00h
          int  21h
code ends
end start
```

结果：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/5d454baece004d82f6ab48e86b52442f.png)
