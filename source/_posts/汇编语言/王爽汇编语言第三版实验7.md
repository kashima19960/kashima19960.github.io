---
title: 王爽汇编语言第三版实验7
date: 2024-10-05 00:13:40
tags: 汇编语言
---

## 前言

本系列的文章是对王爽老师的汇编语言中的实验的解答记录，原书一共有17个实验，由于学校的教学流程只做到了第12个实验，因此本文章只会有前十二个实验的解答记录,还有个比较重要的是，文章中会有原书实验中没有的题目，这些是我的老师自己加的，你不感兴趣就无视。btw，这也算记录我汇编语言的学习历程了。

### 目录

* {% post_link '汇编语言/王爽汇编语言第三版实验1' '王爽汇编语言第三版实验1' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验2' '王爽汇编语言第三版实验2' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验3' '王爽汇编语言第三版实验3' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验4' '王爽汇编语言第三版实验4' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验5' '王爽汇编语言第三版实验5' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验6' '王爽汇编语言第三版实验6' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验7' '王爽汇编语言第三版实验7' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验8' '王爽汇编语言第三版实验8' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验9' '王爽汇编语言第三版实验9' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验10' '王爽汇编语言第三版实验10' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验11' '王爽汇编语言第三版实验11' %}
* {% post_link '汇编语言/王爽汇编语言第三版实验12' '王爽汇编语言第三版实验12' %}

## 实验任务

1.  课本实验7 寻址方式在数据化数据访问中的应用

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/6f99d53e84dcd5b92e88c59fdc4ab013.png)

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/f8ef2c44c9269a5cb81fcfd25f764f40.png)

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/c5dd4c031e73e6e2555d6255005a030e.png)

解答：

```assembly
assume cs:code
data segment
      ;1B
           db '1975','1976','1977','1978','1979','1980','1981','1982','1983','1984','1985'
           db '1986','1987','1988','1989','1990','1991','1992','1993','1994','1995'
      ;4B
           dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514,345980
           dd 590827,803530,1183000,1843000,2758000,3753000,4649000,5937000
      ;2B
           dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226
           dw 11542,14430,15257,17800
data ends
stack segment
            dw 0,0,0,0,0,0,0
stack ends
table segment
            db 21 dup ('year summ ne ?? ')
table ends
;总体思路，分两个循环来进行，第一个循环是将数据段中的年份，收入复制到table段中，
;第二个循环是复制雇员数并计算出人均收入，然后再将结果复制到table段中
code segment
      start:
            mov  ax,data
            mov  es,ax
            mov  si,0
            mov  ax,table
            mov  ds,ax
            mov  di,0
            mov  ax,stack
            mov  ss,ax
            mov  sp,16
            mov  bx,0
            mov  cx,21
      ;es 表示数据段的内容，ds表示table段的内容，
      s0:   
            mov  ax,es:[si]
            mov  ds:[di+bx],ax
            add  si,2
            add  di,2
            mov  ax,es:[si]
            mov  ds:[di+bx],ax
            mov  ax,es:[si+82]
            mov  ds:[bx+di+3],ax
            mov  ax,es:[si+84]
            mov  ds:[bx+di+5],ax
            add  si,2
            mov  di,0
            add  bx,16                      ;将偏移地址变成下一行
            loop s0
            mov  bx,0
            mov  si,168                     ;雇员数的偏移地址
            mov  di,5                       ;收入的偏移地址
            mov  cx,21
      s1:   
            mov  ax,es:[si]
            mov  ds:[bx+di+5],ax
            mov  ax,ds:[bx+di]
            mov  dx,ds:[bx+di+2]
            div  word ptr ds:[bx+di+5]
            mov  ds:[bx+di+8],ax
            add  si,2
            add  bx,16
            loop s1
            mov  ax,4c00h
            int  21h
code ends
end start

```

程序运行前的table段：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/81474095daa45629c9eae4b87d9688fb.png)

程序运行后的table段：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/60ef741563a58249b59472c69817027b.png)

2.   数据段的第一个串为学生本人的姓名拼音小写，转变为大写字母后存入它后面的数据区。例如

DATA SEGEMENT

NAME1 DB ‘liweihua’

NAME2 DB 8 DUP (0)

DATA ENDS

```assembly
assume cs:code, ds:data, ss:stack
data segment
      name1 db 'liweihua'
      name2 db 8 dup (0)
data ends
stack segment
            dw 0,0,0,0,0,0,0,0
stack ends
code segment
      start:
            mov  ax,data              ;初始化栈段和数据段
            mov  ds,ax
            mov  ax,stack
            mov  ss,ax
            mov  sp,10h
            mov  si,offset name1
            mov  bx,offset name2
            sub  bx,si                ;name1 - name2 得到name1长度
            mov  cx,bx                ;循环name1的长度次数
      s:    mov  al,[si]              ;将 ds:si 复制到al
            and  al,11011111B         ;与运算，将小写转变为大写
            mov  [bx+si],al           ;存到name2处数据段
            inc  si                   ;si自增，指向下一个字母
            loop s
            mov  ax,4c00h
            int  21H
code ends
end start

```

程序运行前结果：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/3f82efc8152788df165efd113604afd4.png)

程序运行后结果：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/c93953468dd2db62cb8d9dc6eba984fe.png)

3.   定义下面的4行4列矩阵数组，要求将上三角的所有元素累加，结果存入SUM。

X1   DB 10，20，30，40

DB 20，30，40,50

DB 30，40,50,60

DB 40，50，60，70

SUM DW 0

```assembly
assume cs:codesg,ds:data
DATA segment
           DB 10,20,30,40
           DB 20,30,40,50
           DB 30,40,50,60
           DB 40,50,60,70
      SUM  DW 0
DATA ENDS
codesg segment
      START: 
             MOV  ax,DATA
             MOV  ds,ax              ;将DS指向DATA段
             mov  ax,0
             mov  bx,0
             mov  dx,0
             mov  di,4               ;矩阵有4列
             mov  cx,4               ;矩阵有4行，外循环为4次
      s:     push cx                 ;将CX入栈
             mov  si,0               ;SI表示列
             mov  cx,di              ;内循环每次少加一个数（列），每次少循环一次
      s0:    mov  dl,ds:[bx+si]      ;将矩阵上三角每行从第一个数放入DL中
             add  ax,dx              ;AX=AX+DX,将数字相加后放入AX中
             inc  si                 ;进行下一列的操作
             loop s0
             sub  di,1               ;内循环每次少加一个数（列），每次少循环一次
             add  bx,5               ;指向矩阵上三角每行第一个数
             pop  cx                 ;将CX出栈
             loop s
             mov  [SUM],ax           ;将相加的结果存入SUM中
             MOV  AH,4CH
             INT  21H
CODESG ENDS
END START

```

程序运行前：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/02802651f52c950f9ae630a966cb93f0.png)

程序运行后：

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/ff408c7aa14f7bed7ace9380e965f376.png)

这个结果与手算的结果相一致，因此程序运行正确

4.   杨辉三角

创建一个10行16列的字节数组，并按照杨辉三角的数学规律给数组赋值。杨辉三角形状如下：

1

1 1

1 2 1

1 3 3 1

1 4 6 4 1

1 5 10 10 5 1

```assembly
assume cs:code, ss:stack, ds:data
data segment
      ;声明一段10行16列首列数字为1的二维数组
      array db 10 dup( 1, 15 dup(0) )
data ends
stack segment
            dw 8 dup(0)
stack ends
code segment
      start:
            mov  ax,data
            mov  ds,ax
            mov  ax,stack
            mov  ss,ax
            mov  sp,10h            ;初始化数据段和栈段
            mov  ax,1              ;ax用于保存每行需要计算的次数，从第二行需要计算一次开始，每行增加一次
            mov  bx,0              ;bx用于定位行，从第一行开始
            mov  cx,9              ;循环9次计算剩余9行
      ;设正在计算的数字为(i,j)
      s1:   push cx                ;保存第一层循环的状态
            mov  cx,ax             ;第二层循环的次数(即为该行所需计算的数字个数)
            mov  si,0              ;si用于行中的定位，从第一个字节开始
      s2:   mov  dx,[bx+si]        ;复制(i-1,j-1)到dx
            add  dx,[bx+si+1]      ;dx加上(i-1,j)
            add  bx,16             ;bx移动到下一行,即 i+=1
            add  si,1              ;si移动到下一个字,即 j+=1
            mov  [bx+si],dx        ;将dx复制到(i,j)
            sub  bx,16             ;bx移动到上一行,即 i-=1 ,计算下一个数字
            loop s2
            add  bx,16             ;bx移动到下一行,即 i+=1 ,计算下一行的数字
            inc  ax                ;ax自增，下一行要计算的数量比上一行多1个
            pop  cx                ;恢复第一层循环的状态
            loop s1
            mov  ax,4c00h
            int  21H
code ends
end start


```

程序运行前，数据段为全0

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/bcf6e58e5167211e9a087f1dae19988e.png)

程序运行后

![](https://cdn.jsdelivr.net/gh/kashima19960/img@master/%E6%B1%87%E7%BC%96%E8%AF%AD%E8%A8%80/1ec24bbb942524c40d7616df37f3e8c9.png)
